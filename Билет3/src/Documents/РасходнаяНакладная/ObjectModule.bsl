
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//старая методика проведения
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
    //получаем метод списания
	МетодРС = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодРС;
	Если МетодРС = Неопределено Тогда
	    Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "нет метода рс";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	//БлокировкаД
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	//запрос на получение остатков
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
		|	втТЧТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//обход по группировкам
	Пока ВыборкаНоменклатура.Следующий() Цикл
		//если товар
		Если ВыборкаНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
			КоличествоСписать = ВыборкаНоменклатура.Количество; 
			//если не хватает
			Если КоличествоСписать > ВыборкаНоменклатура.КоличествоОстаток Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Нехватает %1 в кол-ве %2шт", 
											ВыборкаНоменклатура.НоменклатураПредставление,
											КоличествоСписать - ВыборкаНоменклатура.КоличествоОстаток);
				Сообщение.Сообщить();
			КонецЕсли;
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			//если хватает
			Выборка = ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() И КоличествоСписать > 0 Цикл
				//расчет текущего количества
			    ТекКоличество = Мин(КоличествоСписать, Выборка.КоличествоОстаток);    
                //расчет текущей себестоимости
				Себестоимость = ?(ТекКоличество=Выборка.КоличествоОстаток, Выборка.СебестоимостьОстаток,
								ТекКоличество / Выборка.КоличествоОстаток * Выборка.СебестоимостьОстаток);
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Номенклатура = Выборка.Номенклатура;
				Движение.Партия = Выборка.Партия;
				Движение.Количество = ТекКоличество;
				Движение.Себестоимость = Себестоимость;
                КоличествоСписать = КоличествоСписать - ТекКоличество;
			КонецЦикла;
		КонецЕсли;
		
		
	КонецЦикла;
	
КонецПроцедуры
