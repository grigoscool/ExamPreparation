
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОборудованиеВЭксплуатиции.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	Движения.ОборудованиеВЭксплуатиции.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	//БлокировкаД
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("СрокГодностиДо", Новый Диапазон(Дата));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//старая метода
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ВводВЭксплуатациюСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧВводимое
		|ИЗ
		|	Документ.ВводВЭксплуатацию.СписокНоменклатуры КАК ВводВЭксплуатациюСписокНоменклатуры
		|ГДЕ
		|	ВводВЭксплуатациюСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧВводимое.Номенклатура КАК Номенклатура,
		|	втТЧВводимое.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧВводимое.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СрокГодностиДо КАК СрокГодностиДо,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
		|	втТЧВводимое.Номенклатура.СрокЭксплуатацииДней КАК СрокЭксплуатацииДней
		|ИЗ
		|	втТЧВводимое КАК втТЧВводимое
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				СрокГодностиДо >= &Дата
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							втТЧВводимое.Номенклатура КАК Номенклатура
		|						ИЗ
		|							втТЧВводимое КАК втТЧВводимое)) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧВводимое.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиНоменклатурыОстатки.СрокГодностиДо УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
		    Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = стршаблон("Не хватает %1 в кол-ве %2ед.",
										ВыборкаНоменклатура.НоменклатураПредставление,
										ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
			Сообщение.Сообщить();
		КонецЕсли;
	    Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		Выборка = ВыборкаНоменклатура.Выбрать();
	    КоличествоСписать = ВыборкаНоменклатура.Количество;
		Пока Выборка.Следующий() И КоличествоСписать > 0 Цикл
			//списание по партиям в порядке срока годности
			ТекКоличество = Мин(КоличествоСписать, Выборка.КоличествоОстаток);  
			Если ТекКоличество = Выборка.КоличествоОстаток  Тогда
				Себестоимость = Выборка.СебестоимостьОстаток;
			Иначе
				Себестоимость  = ТекКоличество / Выборка.КоличествоОстаток * Выборка.СебестоимостьОстаток;
			КонецЕсли;
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.СрокГодностиДо = Выборка.СрокГодностиДо ;
			Движение.Количество = ТекКоличество;
			Движение.Себестоимость = Себестоимость;
            КоличествоСписать = КоличествоСписать - ТекКоличество; 
			
			//запись в РН оборвЭкспл
			Движение = Движения.ОборудованиеВЭксплуатиции.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.СрокГодностиДо = Выборка.СрокГодностиДо ;
			Движение.Количество = ТекКоличество;
			Движение.Себестоимость = Себестоимость;
			Движение.СрокЭксплуатацииДо = Дата + Выборка.СрокЭксплуатацииДней * 24 * 60 * 60;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
