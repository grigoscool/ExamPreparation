
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ОборудованиеВЭксплуатиции.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	Движения.ОборудованиеВЭксплуатиции.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	//БлокировкаД
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.УстановитьЗначение("СрокГодностиДо", Новый Диапазон(,Дата));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать(); 
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОборудованиеВЭксплуатиции");
	ЭлементБлокировки.УстановитьЗначение("СрокГодностиДо", Новый Диапазон(,Дата));
	ЭлементБлокировки.УстановитьЗначение("СрокЭксплуатацииДо", Новый Диапазон(,Дата));
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать(); 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.СрокГодностиДо КАК СрокГодностиДо,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, СрокГодностиДо < КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)) КАК ОстаткиНоменклатурыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОборудованиеВЭксплуатицииОстатки.Номенклатура КАК Номенклатура,
		|	ОборудованиеВЭксплуатицииОстатки.СрокГодностиДо КАК СрокГодностиДо,
		|	ОборудованиеВЭксплуатицииОстатки.СрокЭксплуатацииДо КАК СрокЭксплуатацииДо,
		|	ОборудованиеВЭксплуатицииОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОборудованиеВЭксплуатицииОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток
		|ИЗ
		|	РегистрНакопления.ОборудованиеВЭксплуатиции.Остатки(
		|			&МоментВремени,
		|			СрокГодностиДо < КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
		|				ИЛИ СрокЭксплуатацииДо < КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)) КАК ОборудованиеВЭксплуатицииОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапросаМассив = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапросаМассив[0].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.СрокГодностиДо = Выборка.СрокГодностиДо ;
		Движение.Количество = Выборка.КоличествоОстаток;
		Движение.Себестоимость = Выборка.СебестоимостьОстаток;
	КонецЦикла;
	Выборка = РезультатЗапросаМассив[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
	 	Движение = Движения.ОборудованиеВЭксплуатиции.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.СрокГодностиДо = Выборка.СрокГодностиДо ;
		Движение.Количество = Выборка.КоличествоОстаток;
		Движение.Себестоимость = Выборка.СебестоимостьОстаток;
		Движение.СрокЭксплуатацииДо = Выборка.СрокЭксплуатацииДо;
	КонецЦикла;
	
КонецПроцедуры
