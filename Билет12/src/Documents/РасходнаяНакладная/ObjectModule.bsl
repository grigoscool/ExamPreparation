
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	Движения.Взаиморасчеты.Записывать = Истина;	
	

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Проект", Справочники.Проекты.ПустаяСсылка());
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	//запрос для нахождения аванса
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
		|	ВзаиморасчетыОстатки.Проект КАК Проект,
		|	ВзаиморасчетыОстатки.Валюта КАК Валюта,
		|	ЕСТЬNULL(ВзаиморасчетыОстатки.СуммаВРубляхОстаток, 0) КАК СуммаВРубляхОстаток,
		|	ЕСТЬNULL(ВзаиморасчетыОстатки.СуммаВВалютеОстаток, 0) КАК СуммаВВалютеОстаток
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
		|ГДЕ
		|	ВзаиморасчетыОстатки.Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());  
	//сумма в расхНакл в той валюте что указана в проекте 
	СуммаСписатьВВалюте = СуммаПоДокументу;  
	Валюта = Вспомогательный.ПолучитьВалюту(Проект);
	Если Валюта = Справочники.Валюты.РоссийскийРубль Тогда
		Курс = 1;
	Иначе
		Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Валюта)).Курс;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Курс) ИЛИ Курс = 0 Тогда
		Отказ = Истина;
		Сообщить("нет данных о курсе");
		Возврат;
	КонецЕсли;	 
	СуммаСписатьВРУблях = СуммаСписатьВВалюте * Курс;
	РезультатЗапроса = Запрос.Выполнить();
	//если нашли аванс - списываем с него в рублях
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать(); 
		Выборка.Следующий();	
	    ТекСумма = Мин(СуммаСписатьВРУблях, Выборка.СуммаВРубляхОстаток);
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Проект; 
		Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
		Движение.СуммаВРублях = ТекСумма; 
		Движение.СуммаВВалюте = ТекСумма;
		СуммаСписатьВРУблях = СуммаСписатьВРУблях - ТекСумма;
	КонецЕсли;
    //если аванса не хватило, то зачисляем остаток в долг
	Если СуммаСписатьВРУблях > 0 Тогда
	
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Проект; 
		Движение.Валюта = Валюта;
		Движение.СуммаВРублях = СуммаСписатьВРУблях; 
		Движение.СуммаВВалюте = СуммаСписатьВРУблях / Курс;		
	
	КонецЕсли;


	
КонецПроцедуры
