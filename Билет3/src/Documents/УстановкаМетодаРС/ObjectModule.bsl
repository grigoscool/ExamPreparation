
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	//установка метода РС
	Движения.УчетнаяПолитика.Записывать = Истина;
	Движение = Движения.УчетнаяПолитика.Добавить();
	Движение.Период = Дата;
	Движение.МетодРС = МетодРС;
	//если метод РС по Сред тогда перепроводим все записи в РН остатки с пустой партией
	
	Если МетодРС = Перечисления.УчетнаяПолитика.Сред Тогда
		Движения.ОстаткиНоменклатуры.Записывать = Истина;
		
		//БлокировкаД
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать(); 
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
			|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
			|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
			|	ОстаткиНоменклатурыОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток
			|ИЗ
			|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, ) КАК ОстаткиНоменклатурыОстатки
			|ИТОГИ
			|	СУММА(КоличествоОстаток),
			|	СУММА(СебестоимостьОстаток)
			|ПО
			|	Номенклатура";
		
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			Выборка = ВыборкаНоменклатура.Выбрать();
		
			Пока Выборка.Следующий() Цикл
				// расходуем детальные записи по партиям
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Номенклатура = Выборка.Номенклатура;
				Движение.Партия = Выборка.Партия;
				Движение.Количество = Выборка.КоличествоОстаток;
				Движение.Себестоимость = Выборка.СебестоимостьОстаток;
			КонецЦикла; 
			
			//приходуем итоговые записи по номенклатуреа
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
			Движение.Партия = Документы.ПриходнаяНакладная.ПустаяСсылка();
			Движение.Количество = ВыборкаНоменклатура.КоличествоОстаток;
			Движение.Себестоимость = ВыборкаНоменклатура.СебестоимостьОстаток;

		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

