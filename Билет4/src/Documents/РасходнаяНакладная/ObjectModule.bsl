
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	//Блокировка
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//запрос с контролем остатков и сортировкой складов по приоритету
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Номенклатура.ВидНоменклатуры КАК НоменклатураВидНоменклатуры,
		|	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток,
		|	ВЫБОР
		|		КОГДА ОстаткиНоменклатурыОстатки.Склад = &Склад
		|			ТОГДА -1
		|		ИНАЧЕ ПриоритетСкладовСрезПоследних.Приоритет
		|	КОНЕЦ КАК Приоритет,
		|	втТЧТовары.Количество КАК Количество
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, Номенклатура В
		|			(ВЫБРАТЬ
		|				втТЧТовары.Номенклатура КАК Номенклатура
		|			ИЗ
		|				втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПриоритетСкладов.СрезПоследних(&МоментВремени,) КАК
		|				ПриоритетСкладовСрезПоследних
		|			ПО ОстаткиНоменклатурыОстатки.Склад = ПриоритетСкладовСрезПоследних.Склад
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|ИТОГИ
		|	СУММА(КоличествоОстаток),
		|	СУММА(СебестоимостьОстаток),
		|	МАКСИМУМ(Количество) КАК Количество
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = СтрШаблон("Не хватает %1 в количестве %2.",
										ВыборкаНоменклатура.НоменклатураПредставление,
										ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
			Сообщение.Сообщить();
		КонецЕсли;
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		Пока Выборка.Следующий() И КоличествоСписать > 0 Цикл
			ТекКоличество = Мин(КоличествоСписать, Выборка.КоличествоОстаток);
			Если ТекКоличество = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СебестоимостьОстаток;
			Иначе
				Себестоимость = ТекКоличество / Выборка.КоличествоОстаток * Выборка.СебестоимостьОстаток;
			КонецЕсли;
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Склад = Выборка.Склад;
			Движение.Количество = ТекКоличество;
			Движение.Себестоимость = Себестоимость;	
			КоличествоСписать = КоличествоСписать - ТекКоличество;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры
