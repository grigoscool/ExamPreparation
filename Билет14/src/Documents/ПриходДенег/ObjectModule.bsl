
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	Движения.Взаиморасчеты.Записывать = Истина;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
		|	ВзаиморасчетыОстатки.Накладная КАК Накладная,
		|	ВзаиморасчетыОстатки.Валюта КАК Валюта,
		|	ВЫБОР
		|		КОГДА ВзаиморасчетыОстатки.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.РоссийскийРубль)
		|			ТОГДА ВзаиморасчетыОстатки.СуммаОстаток
		|		ИНАЧЕ ВзаиморасчетыОстатки.СуммаОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)
		|	КОНЕЦ КАК СуммаВРублях,
		|	ВЫБОР
		|		КОГДА ВзаиморасчетыОстатки.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.РоссийскийРубль)
		|			ТОГДА 1
		|		ИНАЧЕ ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)
		|	КОНЕЦ КАК Курс
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСрезПоследних
		|		ПО (КурсыВалютСрезПоследних.Валюта = ВзаиморасчетыОстатки.Валюта)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВзаиморасчетыОстатки.Накладная.МоментВремени";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	СуммаСписать = СуммаОплаты; 
	//списание долгов по накладным в порядке и с разными валютами
	Пока Выборка.Следующий() и СуммаСписать > 0 Цикл
		ТекСумма = Мин(СуммаСписать, Выборка.СуммаВРублях);
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Контрагент = Контрагент;
		Движение.Валюта = Выборка.Валюта;
		Движение.период = Дата;
		Движение.Накладная = Выборка.Накладная;
		Если Выборка.Курс = 0 Тогда
			Отказ = Истина;
		     Сообщить(СтрШаблон("Незаполнен курс валют по %1 за %2", Выборка.Валюта, Дата));
		Иначе	
		     Движение.Сумма = ТекСумма / Выборка.Курс;
		КонецЕсли;
		
		СуммаСписать = СуммаСписать - ТекСумма;
	КонецЦикла;
	Если СуммаСписать > 0 Тогда
	    //если перепата то идет в аванс с пустой ссылкой
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Контрагент = Контрагент;
		Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
		Движение.период = Дата;
		Движение.Накладная = Документы.РасходнаяНакладная.ПустаяСсылка();
		Движение.Сумма = СуммаСписать;
	КонецЕсли;
КонецПроцедуры
