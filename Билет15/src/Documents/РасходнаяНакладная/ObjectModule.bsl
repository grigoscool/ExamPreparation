
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры


Процедура ОбработкаПроведенияОУ(Отказ, РежимПроведения)
	//получение тек методаРС
	МетодРС = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодРС;
	//старая метода
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	Движения.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;  
	Движения.Продажи.Записывать = Истина;
	
	//блок
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Продажи");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//запрос на остатки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	втТЧТовары.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
		|	втТЧТовары.Сумма КАК Сумма
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СебестоимостьОстаток),
		|	МАКСИМУМ(Сумма)
		|ПО
		|	Номенклатура";
	//меняем тест в зависимости от методаРС
	Если МетодРС = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл    
		СебестоимостьСписать = 0;
		//если товар
		Если ВыборкаНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
			//если не хавтает на складе сообщение
			Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
				Отказ = Истина;
			     Сообщение = Новый СообщениеПользователю;
				 Сообщение.Текст = СтрШаблон("Не хватает %1 в кол-ве %2 ед.", 
				 							ВыборкаНоменклатура.НоменклатураПредставление, 
											ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
				 Сообщение.Сообщить();
			КонецЕсли;
		    Если Отказ Тогда
				Продолжить;
			КонецЕсли;			
		
	        //если хватает списывам по партиям
			КоличествоСписать = ВыборкаНоменклатура.Количество;
			
			Выборка = ВыборкаНоменклатура.Выбрать();
		    
			Пока Выборка.Следующий() И КоличествоСписать > 0 Цикл 
				ТекКоличество = Мин(КоличествоСписать, Выборка.КоличествоОстаток);  
				Если ТекКоличество = Выборка.КоличествоОстаток Тогда
					Себестоимость =  Выборка.СебестоимостьОстаток;			
				Иначе
					Себестоимость = ТекКоличество / Выборка.КоличествоОстаток * Выборка.СебестоимостьОстаток;
				КонецЕсли;
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Номенклатура = Выборка.Номенклатура;
				Движение.Партия = Выборка.Партия;
				Движение.Количество = ТекКоличество;
				Движение.Себестоимость = Себестоимость;
				КоличествоСписать = КоличествоСписать - ТекКоличество;
				СебестоимостьСписать = СебестоимостьСписать + Себестоимость;
			КонецЦикла;
		КонецЕсли;
		//запись в обороты итоговых чисел списания
		Движение = Движения.Продажи.Добавить();
		Движение.период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество = ВыборкаНоменклатура.Количество;
		Движение.Себестоимость = СебестоимостьСписать;
		Движение.Продажи = ВыборкаНоменклатура.Сумма;	
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведенияБУ(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаСрезПоследних.МетодРС КАК МетодРС
		|ИЗ
		|	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Период, ) КАК УчетнаяПолитикаСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
	    Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "не указана учетная политика";
		Сообщение.Сообщить();
		Отказ = Истина;
		Возврат;
	КонецЕсли;    
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий(); 
	МетодРС = Выборка.МетодРС;
	
	Движения.Управленческий.Записывать = Истина;
	Движения.Записать();
	Движения.Управленческий.Записывать = Истина;
	
	//блокировкад
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УправленческийОстатки.Субконто1 КАК Номенклатура,
		|	УправленческийОстатки.Субконто2 КАК Склад,
		|	УправленческийОстатки.Субконто3 КАК Партия,
		|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	втТЧТовары.Количество КАК Количество
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		|				&МоментВремени,
		|				Счет = &Счет,
		|				&ВидСубконто,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							втТЧТовары.Номенклатура КАК Номенклатура
		|						ИЗ
		|							втТЧТовары КАК втТЧТовары)
		|					И Субконто2 = &Склад) КАК УправленческийОстатки
		|		ПО втТЧТовары.Номенклатура = УправленческийОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени УБЫВ
		|ИТОГИ
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток),
		|	МАКСИМУМ(Количество)
		|ПО
		|	Номенклатура";     
	
	ВидСубконто = Новый Массив;
	ВидСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	ВидСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
	ВидСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Партии);

	Запрос.УстановитьПараметр("ВидСубконто", ВидСубконто);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Управленческий.Товары);
	
	Если МетодРС = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСубконто1 = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСубконто1.Следующий() Цикл
		КоличествоСписать = ВыборкаСубконто1.Количество;
		
		Если КоличествоСписать > ВыборкаСубконто1.КоличествоОстаток Тогда
		     Сообщение = Новый СообщениеПользователю;
			 Сообщение.Текст = СтрШаблон("Не хватает %1 ед %2",
			 								КоличествоСписать - ВыборкаСубконто1.КоличествоОстаток,
											ВыборкаСубконто1.Номенклатура);
			 Сообщение.Сообщить(); 
			 Отказ = Истина;
		КонецЕсли;
		Если Отказ  Тогда
			Продолжить;
		КонецЕсли; 
		Выборка = ВыборкаСубконто1.Выбрать();
	
		Пока Выборка.Следующий() И КоличествоСписать > 0 Цикл
			ТемпКоличество = Мин(КоличествоСписать, Выборка.КоличествоОстаток);
			
			Если КоличествоСписать = Выборка.КоличествоОстаток Тогда
			   Себестоимость = Выборка.СуммаОстаток;
			Иначе	
			   Себестоимость = ТемпКоличество / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			КонецЕсли;
			
			 Движение = Движения.Управленческий.Добавить();
			 Движение.Период = Дата;
			 Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
			 Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
			 Движение.СубконтоДт.Номенклатура = Выборка.Номенклатура;
			 Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Выборка.Склад;
			 Движение.СубконтоДт.Партии = Выборка.Партия;
			 Движение.КоличествоДт = ТемпКоличество;
			 Движение.Сумма = Себестоимость;
			 
			 КоличествоСписать = КоличествоСписать - ТемпКоличество;
		КонецЦикла;
	КонецЦикла;
	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
	Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
	Движение.Сумма = СуммаПоДокументу;
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ОбработкаПроведенияОУ(Отказ, РежимПроведения);
	ОбработкаПроведенияБУ(Отказ, РежимПроведения);
КонецПроцедуры

