
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры



Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//По старой методе
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;

	//Блокировка
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия", "Партия");
	Блокировка.Заблокировать();
	//Контроль остатков
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия КАК Партия,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Партия КАК Партия,
		|	втТЧТовары.Партия.Представление КАК ПартияПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	втТЧТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
		|	втТЧТовары.Сумма
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, (Партия, Номенклатура) В
		|			(ВЫБРАТЬ
		|				втТЧТовары.Партия КАК Партия,
		|				втТЧТовары.Номенклатура КАК Номенклатура
		|			ИЗ
		|				втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|		И втТЧТовары.Партия = ОстаткиНоменклатурыОстатки.Партия";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СебестоимостьИтого = 0;
		//проверяем что не услуга и что хватает на списание
		Если Выборка.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
			
			Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = СтрШаблон("Не хватает %1 в кол-ве %2шт по партии %3",
											Выборка.НоменклатураПредставление,
											Выборка.Количество - Выборка.КоличествоОстаток,
											Выборка.Партия);
				Сообщение.Сообщить();
			КонецЕсли;
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			//расходуем остатки
			Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СебестоимостьОстаток;
			Иначе
				Себестоимость = Выборка.Количество / Выборка.КоличествоОстаток * Выборка.СебестоимостьОстаток;
			КонецЕсли;
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Партия = Выборка.Партия;
			Движение.Количество = Выборка.Количество;
			Движение.Себестоимость = Себестоимость;
			СебестоимостьИтого = СебестоимостьИтого + Себестоимость;
			
		КонецЕсли;
		//движение по регистру продажи
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		Движение.Себестоимость = СебестоимостьИтого;
		Движение.Продажа = Выборка.Сумма;
		
	КонецЦикла;

КонецПроцедуры
